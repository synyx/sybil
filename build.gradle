apply plugin: 'java' //Java plugin
apply plugin: 'war' //war plugin
apply from: 'https://raw.github.com/akhikhl/gretty/master/pluginScripts/gretty.plugin' //jetty 9 & other cool stuff

sourceCompatibility = 1.8 //Java 8
version = '0.1'

gretty { // configure gretty
    recompileOnSourceChange = false; // not terribly useful with an autosaving IDE.
    contextPath = "/" // make the project available at / URL instead of /$projectname/
    logDir = "logs" // put server logs in the same place as app logs
    logFileName = "server" // sybil.log is already used by the app log
    integrationTestTask = 'integTest'
}

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/libs-release" }
}

sourceSets {
    integTest { //add sources for Integration Tests, unlike main/ and test/ these are not provided by the java plugin
        java.srcDir file('src/integTest/java')
        resources.srcDir file('src/integTest/resources')
    }
}

task integTest(type: Test) { //define the integTest task as a Test task
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath

    systemProperty 'logLevel', 'DEBUG' //enable debug logging for tests
    systemProperty 'logDirectory', 'build/logs/' //set log directory 
    systemProperty 'spring.profiles.active', 'dev' //set active Spring profile to development
    //testLogging.showStandardStreams = true //show STDOUT and STDERR while gradle is running
}

//uncomment to run integration tests with the build task. Check is a subtask of the build task, provided by java plugin
//check.dependsOn integTest

//Unit tests
test {
    systemProperty 'logLevel', 'DEBUG' //enable debug logging for tests
    systemProperty 'logDirectory', 'build/logs/' //set log directory 
    systemProperty 'spring.profiles.active', 'dev'  //set active Spring profile to development
//    testLogging.showStandardStreams = true //show STDOUT and STDERR while gradle is running
}

dependencies {
    //spring
    compile group: 'org.springframework', name: 'spring-web', version: '4.1.4.RELEASE'
    compile group: 'org.springframework.data', name: 'spring-data-neo4j', version: '3.2.2.RELEASE'
    compile group: 'org.springframework.hateoas', name: 'spring-hateoas', version: '0.16.0.RELEASE'
    compile group: 'org.springframework.plugin', name: 'spring-plugin-core', version: '1.1.0.RELEASE'
    compile group: 'javax.validation', name: 'validation-api', version: '1.1.0.Final'
    compile group: 'org.hibernate', name: 'hibernate-validator', version: '5.1.3.Final'
    providedCompile group: 'javax', name: 'javaee-web-api', version: '7.0'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.10'

    //json
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.5.0'
    compile group: 'com.jayway.jsonpath', name: 'json-path', version: '1.2.0'
    
    //tinkerforge
    compile group: 'com.tinkerforge', name: 'tinkerforge', version: '2.1.4'

    //logging
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.2'
    compile group: 'ch.qos.logback.contrib', name: 'logback-json-classic', version: '0.1.2'
    compile group: 'ch.qos.logback.contrib', name: 'logback-jackson', version: '0.1.2'
    
    //testing dependencies
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.springframework', name: 'spring-test', version: '4.1.4.RELEASE'

    //make sure the integTest task has the same dependencies
    integTestCompile sourceSets.main.output
    integTestCompile configurations.testCompile
    integTestCompile sourceSets.test.output
    integTestRuntime configurations.testRuntime
}
