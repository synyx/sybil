apply plugin: 'java'
apply plugin: 'war'

sourceCompatibility = 1.8
version = '0.1'

repositories {
    mavenCentral()
}

//Integration tests
sourceSets {
    integTest {
        java.srcDir file('src/integTest/java')
        resources.srcDir file('src/integTest/resources')
    }
}

task integTest(type: Test) {
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath

    systemProperty 'logLevel', 'DEBUG'
    systemProperty 'logDirectory', 'build/logs/'
    systemProperty 'spring.profiles.active', 'dev'
}

//uncomment to run integration tests with the build task
//check.dependsOn integTest

//Unit tests
test {
    //enable debug logging for tests
    systemProperty 'logLevel', 'DEBUG'
    systemProperty 'logDirectory', 'build/logs/'
    systemProperty 'spring.profiles.active', 'dev'
//    testLogging.showStandardStreams = true
}

dependencies {
    compile group: 'org.springframework', name: 'spring-web', version: '4.1.3.RELEASE'
    compile group: 'com.tinkerforge', name: 'tinkerforge', version: '2.1.2'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.2'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.springframework', name: 'spring-test', version: '4.1.3.RELEASE'

    integTestCompile sourceSets.main.output
    integTestCompile configurations.testCompile
    integTestCompile sourceSets.test.output
    integTestRuntime configurations.testRuntime
}